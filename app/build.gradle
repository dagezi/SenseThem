apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "io.github.dagezi.sensethem"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        releaseConfig {
            storeFile file('release.jks')
        }
        debugConfig {
            storeFile file('debug.jks')
            storePassword 'dagezi'
            keyPassword 'dagezi'
            keyAlias 'debug'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debugConfig
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    testCompile 'junit:junit:4.12'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

if (keystorePropertiesFile.exists()) {
    def properties = new Properties()
    properties.load(new FileInputStream(keystorePropertiesFile))

    android.signingConfigs.releaseConfig.storePassword = properties['storePassword']
    android.signingConfigs.releaseConfig.keyPassword = properties['keyPassword']
    android.signingConfigs.releaseConfig.keyAlias = properties['keyAlias']
} else {
    android.buildTypes.release.signingConfig = null
}
